include "/home/vivianyyd/type-synth/src/main/sketch/util.sk";

adt Type {
    Variable { int tid; int vid; }
    Label { int l; List@list<Type> params; }
    Function { Type left; Type rite; }
}

struct Binding { int tid; int vid; Type t; }

Type applyBinding(Type t, int tid, int vid, Type sub) {
    switch (t) {
        case Label:{
            return new Label(l=t.l, params=applyBindingToAll(t.params, tid, vid, sub));
        }
        case Function:{
            return new Function(left=applyBinding(t.left, tid, vid, sub), rite=applyBinding(t.rite, tid, vid, sub));
        }
        case Variable:{
            if (t.tid == tid && t.vid == vid) return sub;
            else return t;
        }
    }
}

List@list<Type> applyBindingToAll(List@list<Type> types, int tid, int vid, Type sub) {
    List@list<Type> result = empty();
    switch (types) {
        case Cons:{
            List@list<Type> tmp = add(result, applyBinding(types.val, tid, vid, sub));
            return tmp;
        }
        case Nil:{ return result; }
    }
}

Type applyBindings(Type t, List@list<Binding> bindings) {
    switch (bindings) {
        case Cons:{ return applyBindings(applyBinding(t, bindings.val.tid, bindings.val.vid, bindings.val.t), bindings.next); }
        case Nil:{ return t; }
    }
}

List@list<Type> applyBindingsToAll(List@list<Type> types, List@list<Binding> bindings) {
    List@list<Type> result = empty();
    switch (types) {
        case Cons:{ return add(result, applyBindings(types.val, bindings)); }
        case Nil:{ return result; }
    }
}

List@list<Binding> unifyAll(List@list<Type> params, List@list<Type> args) {
    assert(len(params) == len(args));
    switch (params) {
        case Cons:{
            switch (args) {
                case Cons:{
                    List@list<Binding> bindings = unify(params.val, args.val);
                    if (bindings == null) return null;
                    List@list<Type> newParams = applyBindingsToAll(params.next, bindings);
                    List@list<Binding> remainingBindings = unifyAll(newParams, args.next);
                    if (remainingBindings == null) return null;
                    return plus(bindings, remainingBindings);
                }
                case Nil:{ assert false; }
            }
        }
        case Nil:{
            switch (args) {
                case Cons:{ assert false; }
                case Nil:{ return empty(); }
            }
        }
    }
}

/** Returns a list of bindings resulting from unifying [arg] with [param], or null if they are incompatible. */
List@list<Binding> unify(Type param, Type arg) {
    switch(param) {
        case Variable:{
            Binding b = new Binding(tid=param.tid, vid=param.vid, t=arg);
            return single(b);
        }
        case Label:{
            switch(arg) {
                case Variable:{ assert false; }  // TODO I think invariant. If we had []: L<a> this would be different
                case Label:{
                    if (param.l != arg.l || len(param.params) != len(arg.params)) return null;
                    return unifyAll(param.params, arg.params);
                }
                case Function:{ return null; }
            }
        }
        case Function:{
            switch(arg) {
                case Variable:{ assert false; }  // TODO I think invariant
                case Label:{ return null; }
                case Function:{
                    List@list<Binding> leftBindings = unify(param.left, arg.left);
                    if (leftBindings == null) return null;
                    Type newParamRight = applyBindings(param.rite, leftBindings);
                    List@list<Binding> riteBindings = unify(newParamRight, arg.rite);
                    if (riteBindings == null) return null;
                    return plus(leftBindings, riteBindings);
                }
            }
        }
    }
}

/**
 Returns the output type of [fn] on input [arg] with no free variables, or null if [arg] is invalid for [fn].
 */
Type apply(Function fn, Type arg) {
    List@list<Binding> b = unify(fn.left, arg);
    if (b == null) return null;
    return applyBindings(fn.rite, b);
}

boolean isFunction(Type t) {
    switch(t) {
        case Variable:{ return false; }  // TODO think about this
        case Label:{ return false; }
        case Function:{ return true; }
    }
}

struct LabelKind { int l; int numParams; }

LabelKind kind(Label l) {
    return new LabelKind(l=l.l, numParams=len(l.params));
}

List@list<LabelKind> registerLabel(List@list<LabelKind> register, Label label) {
    if (in(register, kind(label))) return register;
    else return add(register, kind(label));
}

generator Type type(List@list<LabelKind> register, int tid, int vars, ref List@list<LabelKind> outRegister, ref int outVars) {
    int t = ??;
    if (t == 0) {
        outRegister = register;
        return variable(tid, vars, outVars);
    }
    if (t == 1) {
        return label(register, tid, vars, outRegister, outVars);
    }
    if (t == 2) {
        List@list<LabelKind> tmpRegister;
        int tmpVars;
        Type a = type(register, tid, vars, tmpRegister, tmpVars);
        Type b = type(tmpRegister, tid, tmpVars, outRegister, outVars);
        return new Function(left=a, rite=b);
    }
    assert false;
}

generator Label label(List@list<LabelKind> register, int tid, int vars, ref List@list<LabelKind> outRegister, ref int outVars) {
    int nParams = ??;
    minimize(nParams);  // want weaker min than min(len(register))
    List@list<Type> params = empty();
    List@list<LabelKind> currRegister = register;
    int currVars = vars;
    for (int i = 0; i < nParams; i++) {
        List@list<LabelKind> nextRegister;
        int nextVars;
        params = add(params, type(currRegister, tid, currVars, nextRegister, nextVars));
        currRegister = nextRegister;
        currVars = nextVars;
    }
    Label o = new Label(l=??, params=params);
    outRegister = registerLabel(currRegister, o);
    outVars = currVars;
    return o;
}

generator Label clabel(List@list<LabelKind> register, int tid, int vars, ref List@list<LabelKind> outRegister, ref int outVars) {
    int nParams = ??;
    minimize(nParams);  // want weaker min than min(len(register))
    List@list<Type> params = empty();
    List@list<LabelKind> currRegister = register;
    int currVars = vars;
    for (int i = 0; i < nParams; i++) {
        List@list<LabelKind> nextRegister;
        int nextVars;
        params = add(params, ctype(currRegister, tid, currVars, nextRegister, nextVars));
        currRegister = nextRegister;
        currVars = nextVars;
    }
    Label o = new Label(l=??, params=params);
    outRegister = registerLabel(currRegister, o);
    outVars = currVars;
    return o;
}
generator Type ctype(List@list<LabelKind> register, int tid, int vars, ref List@list<LabelKind> outRegister, ref int outVars) {
    int t = ??;
    if (t == 1) {
        return clabel(register, tid, vars, outRegister, outVars);
    }
    if (t == 2) {
        List@list<LabelKind> tmpRegister;
        int tmpVars;
        Type a = type(register, tid, vars, tmpRegister, tmpVars);
        Type b = type(tmpRegister, tid, tmpVars, outRegister, outVars);
        return new Function(left=a, rite=b);
    }
    assert false;
}
generator Variable variable(int tid, int vars, ref int outVars) {
    bit b = ??;
    if (b) {
        outVars = vars + 1;
        return new Variable(tid=tid, vid=vars);
    }
    else {
        int v = ??;
        assert (v < vars);
        outVars = vars;
        return new Variable(tid=tid, vid=v);
    }
    minimize(b);
}

generator Variable variableInRange(int tid, int minV, int maxV) {
    int v = ??;
    assert (minV <= v  && v < maxV);
    return new Variable(tid=tid, vid=v);
}
