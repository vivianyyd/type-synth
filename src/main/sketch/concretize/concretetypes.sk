include "/home/vivianyyd/type-synth/src/main/sketch/util.sk";

adt Type {
    Variable { int tid; int vid; }
    Label { int l; List<Type> params; }
    Function { Type left; Type rite; }
}

struct Binding { int tid; int vid; Type t; }

Type applyBinding(Type t, int tid, int vid, Type sub) {
    switch (t) {
        case Label:{
            return new Label(l=t.l, params=applyBindingToAll(t.params, tid, vid, sub));
        }
        case Function:{
            return new Function(left=applyBinding(t.left, tid, vid, sub), rite=applyBinding(t.rite, tid, vid, sub));
        }
        case Variable:{
            if (t.tid == tid && t.vid == vid) return sub;
            else return t;
        }
    }
}

List<Type> applyBindingToAll(List<Type> types, int tid, int vid, Type sub) {
    List<Type> result = empty();
    switch (types) {
        case Cons:{ add(result, applyBinding(types.val, tid, vid, sub)); }
        case Nil:{ return result; 
    }
}

Type applyBindings(Type t, List<Binding> bindings) {
    switch (bindings) {
        case Cons:{ return applyBindings(applyBinding(t, bindings.val.tid, bindings.val.vid, bindings.val.t), bindings.next); }
        case Nil:{ return t; }
    }
}

List<Type> applyBindingsToAll(List<Type> types, List<Binding> bindings) {
    List<Type> result = empty();
    switch (types) {
        case Cons:{ add(result, applyBindings(types.val, bindings)); }
        case Nil:{ return result; }
    }
}

List<Binding> unifyAll(List<Type> params, List<Type> args) {
    assert(len(params) == len(args));
    switch (params) {
        case Cons:{
            switch (args) {
                case Cons:{
                    List<Binding> bindings = unify(params.val, args.val);
                    if (bindings == null) return null;
                    List<Type> newParams = applyBindingsToAll(params.next, bindings);
                    List<Binding> remainingBindings = unifyAll(newParams, args.next);
                    if (remainingBindings == null) return null;
                    return plus(bindings, remainingBindings);
                }
                case Nil:{ assert false; }
            }
        }
        case Nil:{
            switch (args) {
                case Cons:{ assert false; }
                case Nil:{ return empty(); }
            }
        }
    }
}

/** Returns a list of bindings resulting from unifying [arg] with [param], or null if they are incompatible. */
List<Binding> unify(Type param, Type arg) {
    switch(param) {
        case Variable:{
            Binding b = new Binding(tid=param.tid, vid=param.vid, t=arg);
            return single(b);
        }
        case Label:{
            switch(arg) {
                case Variable:{ assert false; }  // TODO I think invariant. If we had []: L<a> this would be different
                case Label:{
                    if (param.l != arg.l || len(param.params) != len(arg.params)) return null;
                    return unifyAll(param.params, arg.params);
                }
                case Function:{ return null; }
            }
        }
        case Function:{
            switch(arg) {
                case Variable:{ assert false; }  // TODO I think invariant
                case Label:{ return null; }
                case Function:{
                    List<Binding> leftBindings = unify(param.left, arg.left);
                    if (leftBindings == null) return null;
                    Type newParamRight = applyBindings(param.rite, leftBindings);
                    List<Binding> riteBindings = unify(newParamRight, arg.rite);
                    if (riteBindings == null) return null;
                    return plus(leftBindings, riteBindings);
                }
            }
        }
    }
}

/**
 Returns the output type of [fn] on input [arg] with no free variables, or null if [arg] is invalid for [fn].
 */
Type apply(Function fn, Type arg) {
    List<Binding> b = unify(fn.left, arg);
    if (b == null) return null;
    return applyBindings(fn.rite, b);
}

boolean isFunction(Type t) {
    switch(t) {
        case Variable:{ return false; }  // TODO think about this
        case Label:{ return false; }
        case Function:{ return true; }
    }
}
