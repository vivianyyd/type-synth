adt Type {
    ConcreteLabel { int dummy; }
    VarBind { int id; }
    VarRef { int id; }
    VarLabelBound { }
    Label { }
    Function { Type left; Type rite; }
}

struct Binding { int id; Type t; }

Type applyBinding(Type t, int varId, Type sub) {
    switch (t) {
        case ConcreteLabel:{ return t; }
        case VarLabelBound:{ return t; }
        case Label:{ return t; }
        case Function:{
            return new Function(left=applyBinding(t.left, varId, sub), rite=applyBinding(t.rite, varId, sub));
        }
        case VarBind:{ return t; }
        case VarRef:{
            if (t.id == varId) return sub;
            else return t;
        }
    }
}

Type applyBindings(Type t, List<Binding> bindings) {
    switch (bindings) {
        case Cons:{ return applyBindings(applyBinding(t, bindings.val.id, bindings.val.t), bindings.next); }
        case Nil:{ return t; }
    }
}

// TODO good test: V -> V doesn't work for cons after binding logic (did it before?)

// TODO I think we can just replace all the return nulls with assert falses

/** Returns a list of bindings resulting from unifying [arg] with [param], or null if they are incompatible. */
List<Binding> unify(Type param, Type arg) {
    // TODO for non-nullary (and nullary), can also add harnesses for subexpr type eq/neq (or unify?? table this..)
    //      it's not always explicitly the type of a subexpression that gets bound to a type variable (idk where I was going w this)

    // TODO new test file which tests that (a -> a) applied with (L -> L) is a function
    //   and Not trivial - Need to know what a is bound to in (a->b)->a
    switch(param) {
        case VarBind:{
            // TODO avoid variable capture!!! minor bug in encoding rn:
            //   numVarsNextId in generator needs to be a global variable varCount. Then need numVars
            //   a separate variable inside each generator to count vars inside. varnames are local + global
            Binding b = new Binding(id=param.id, t=arg);
            return single(b);
        }
        case VarLabelBound:{ return empty(); }
        case ConcreteLabel:{
            switch(arg) {
                case ConcreteLabel:{
                    if (param.dummy == arg.dummy) return empty();
                    return null;
                }
                case VarLabelBound:{ return empty(); }
                case Label:{ return empty(); }
                case Function:{ return null; }
                // Invariants
                case VarBind:{ assert false; }
                case VarRef:{ assert false; }
            }
        }
        case Label:{
            switch(arg) {
                case ConcreteLabel:{ return empty(); }
                case VarLabelBound:{ return empty(); }
                case Label:{ return empty(); }
                case Function:{ return null; }
                // Invariants
                case VarBind:{ assert false; }
                case VarRef:{ assert false; }
            }
        }
        case Function:{
            switch(arg) {
                case VarLabelBound:{ return empty(); }
                case Label:{ return null; }
                case ConcreteLabel:{ return null; }
                case Function:{
                    List<Binding> leftBindings = unify(param.left, arg.left);
                    if (leftBindings == null) return null;
                    Type newParamRight = applyBindings(param.rite, leftBindings);
                    List<Binding> riteBindings = unify(newParamRight, arg.rite);
                    if (riteBindings == null) return null;
                    return plus(leftBindings, riteBindings);
                }
                // Invariants
                case VarBind:{ assert false; }
                case VarRef:{ assert false; }
            }
        }
        case VarRef:{ assert false; }  // Invariant
    }
}

/**
 Returns the output type of [fn] on input [arg] with no free variables, or null if [arg] is invalid for [fn].
 */
Type apply(Function fn, Type arg) {
    //   TODO These invariants could be enforced in the grammar but whatever for now
    //   types should never be a VarRef here! resolve all types upon application
    //   types should never be a VarBind at the top level! can only be under left side of fns
    //   I think arg can't be VarRef either
    Type param = fn.left;

    // Assert invariants before short circuiting logic
    switch(arg) {
        case VarBind:{ assert false; }
        case VarRef:{ assert false; }
        case VarLabelBound:{ }
        case Label:{ }
        case ConcreteLabel:{ }
        case Function:{ }
    }

    List<Binding> b = unify(param, arg);
    if (b == null) return null;
    return applyBindings(fn.rite, b);
    // Invariant: returned type never contains VarRef!!!! think about this..

}

boolean isFunction(Type t) {
    switch(t) {
        case VarBind:{ return false; }
        case VarRef:{ return false; }
        case VarLabelBound:{ return false; }
        case Label:{ return false; }
        case ConcreteLabel:{ return false; }
        case Function:{ return true; }
    }
}

int len <T> (List<T> l) {
    switch(l){
        case Cons:{ return 1 + len(l.next); }
        case Nil:{ return 0; }
    }
}

/** ith element, or null if [i >= len(l)] */
T get <T> (List<T> l, int i) {
    switch(l){
        case Cons:{
            if (i == 0) return l.val;
            else return get(l.next, i - 1);
        }
        case Nil:{ return null; }
    }
}

List<T> plus <T> (List<T> l, List<T> elems) {
    switch (elems) {
        case Cons:{
            return plus(add(l, elems.val), elems.next);
        }
        case Nil:{ return l; }
    }
}
